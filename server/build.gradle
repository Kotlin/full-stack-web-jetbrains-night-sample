plugins {
    id 'java'
    id 'org.jetbrains.kotlin.jvm'
    id 'kotlinx-serialization'
    id 'application'
    id 'distribution'
}

repositories {
    maven { url 'https://dl.bintray.com/kotlin/kotlin-dev' }
    maven { url 'https://dl.bintray.com/kotlin/kotlin-eap' }
    jcenter()
    maven { url "https://dl.bintray.com/kotlin/kotlin-js-wrappers" }
    maven { url "https://dl.bintray.com/kotlin/kotlinx" }
    maven { url "https://dl.bintray.com/kotlin/exposed" }
    mavenCentral()
}

def kotlin_version = 'kotlin-1.3.70' // for kotlin-wrappers
def ktor_version = '1.3.2'
def logback_version = '1.2.3'
def kotlinx_serialization_version = '0.20.0'

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
    implementation "io.ktor:ktor-server-netty:$ktor_version"
    implementation "io.ktor:ktor-client-apache:$ktor_version"
    implementation "io.ktor:ktor-jackson:$ktor_version"
    implementation "io.ktor:ktor-html-builder:$ktor_version"
    implementation "ch.qos.logback:logback-classic:$logback_version"
    implementation "org.jetbrains:kotlin-css:1.0.0-pre.94-kotlin-$kotlin_version"
    implementation "org.jetbrains.kotlinx:kotlinx-serialization-runtime:$kotlinx_serialization_version"
    implementation "org.jetbrains.exposed:exposed-core:0.22.1"
    implementation "org.jetbrains.exposed:exposed-dao:0.22.1"
    implementation "org.jetbrains.exposed:exposed-jdbc:0.22.1"
    implementation 'com.h2database:h2:1.4.200'

    implementation project(":shared")

    testImplementation "org.jetbrains.kotlin:kotlin-test"
    testImplementation "org.jetbrains.kotlin:kotlin-test-junit"
}

application {
    mainClassName = 'io.ktor.server.netty.EngineMain'
}

processResources {
    from project(":client").tasks.getByName('browserProductionWebpack').destinationDirectory

    //todo: bug?
    dependsOn project(":client").tasks.getByName('browserProductionWebpack')
}


////// Dev server

task devServerResources(type: Copy) {
    destinationDir = new File(project.buildDir, "dev-resources")
    from sourceSets.main.resources
    filter { line -> line.replaceAll('port = 8080', 'port = 8081') }
}

task prepareDevServer {
    dependsOn "compileKotlin"
    dependsOn "devServerResources"
}

task devServer(type: JavaExec) {
    dependsOn "prepareDevServer"

    classpath = project.files(
            configurations.runtimeClasspath,
            new File(project.buildDir, "classes/kotlin/main"),
            new File(project.buildDir, "dev-resources")
    )
    main = 'io.ktor.server.netty.EngineMain'
}

///////////////