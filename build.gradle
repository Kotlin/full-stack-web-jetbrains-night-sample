buildscript {
    repositories {
        jcenter()
    }
}

plugins {
    id 'kotlin-multiplatform' version '1.3.21'
    id 'kotlinx-serialization' version '1.3.21'
    id 'org.jetbrains.kotlin.frontend' version '0.0.45'
}

repositories {
    jcenter()
    maven { url "https://dl.bintray.com/kotlin/ktor" }
    maven { url "https://dl.bintray.com/kotlin/kotlin-js-wrappers" }
    maven { url "https://dl.bintray.com/kotlin/kotlinx" }
    mavenCentral()
}

def kotlin_version = '1.3.21'
def ktor_version = '1.1.3'
def logback_version = '1.2.3'

kotlin {
    jvm()
    js() {
        compilations.all {
            kotlinOptions {
                languageVersion = "1.3"
                moduleKind = "commonjs"
                sourceMap = true
                sourceMapEmbedSources = "always"
                metaInfo = true
            }
        }

        compilations.main {
            tasks.getByName(compileKotlinTaskName).kotlinOptions {
                main = "call"
            }
        }
    }

    sourceSets {
        commonMain {
            dependencies {
                implementation kotlin('stdlib-common')
                implementation "org.jetbrains:kotlin-css:1.0.0-pre.69-kotlin-$kotlin_version"
                implementation "org.jetbrains.kotlinx:kotlinx-serialization-runtime-common:0.10.0"
            }
        }
        commonTest {
            dependencies {
                implementation kotlin('test-common')
                implementation kotlin('test-annotations-common')
            }
        }
        jvmMain {
            dependencies {
                implementation kotlin('stdlib-jdk8')
                implementation "io.ktor:ktor-server-netty:$ktor_version"
                implementation "io.ktor:ktor-client-apache:$ktor_version"
                implementation "io.ktor:ktor-html-builder:$ktor_version"
                implementation "ch.qos.logback:logback-classic:$logback_version"
                implementation "org.jetbrains:kotlin-css-jvm:1.0.0-pre.69-kotlin-$kotlin_version"
                implementation "org.jetbrains.kotlinx:kotlinx-serialization-runtime:0.10.0"
                implementation "org.jetbrains.exposed:exposed:0.13.2"
            }
        }
        jvmTest {
            dependencies {
                implementation kotlin('test')
                implementation kotlin('test-junit')
            }
        }
        jsMain {
            dependencies {
                implementation kotlin('stdlib-js')
                implementation "org.jetbrains:kotlin-extensions:1.0.1-pre.69-kotlin-$kotlin_version"
                implementation "org.jetbrains:kotlin-css-js:1.0.0-pre.69-kotlin-$kotlin_version"
                implementation "org.jetbrains:kotlin-react:16.6.0-pre.69-kotlin-1.3.21"
                implementation "org.jetbrains:kotlin-react-dom:16.6.0-pre.69-kotlin-1.3.21"
                implementation "org.jetbrains:kotlin-styled:1.0.0-pre.69-kotlin-1.3.21"
                implementation "org.jetbrains.kotlinx:kotlinx-serialization-runtime-js:0.10.0"
            }
        }
        jsTest {
            dependencies {
                implementation kotlin('test-js')
            }
        }
    }
}

kotlinFrontend {
    npm {
        dependency("core-js", "^2.0.0")
        dependency("svg-inline-loader", "0.8.0")

        dependency("@jetbrains/kotlin-extensions", "1.0.1-pre.67")
        dependency("@jetbrains/kotlin-css", "1.0.0-pre.67")
        dependency("@jetbrains/kotlin-css-js", "1.0.0-pre.67")

        dependency("react", "16.8.3")
        dependency("react-dom", "16.8.3")
        dependency("inline-style-prefixer", "5.0.4")
        dependency("styled-components", "3.4.10")

        dependency("@jetbrains/logos", "1.1.4")
        dependency("@jetbrains/ring-ui", "2.0.0-beta.11")
    }

    sourceMaps = false

    webpackBundle {
        port = 8080
        bundleName = "main"
        proxyUrl = "http://0.0.0.0:8081"
        sourceMapEnabled = true
        //mode = "development"
        mode = "production"
    }
}

ktor {
    port = 8081
    mainClass = "io.ktor.server.netty.EngineMain"
}
